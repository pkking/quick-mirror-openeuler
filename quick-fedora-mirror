#!/bin/zsh
LANG=C
# Simple script to grab the file list from Fedora and rsync everything that's
# changed since the last time we pulled.
#
# Originally written by Jason Tibbitts <tibbs@math.uh.edu> in 2016.
# Donated to the public domain.  If you require a statement of license, please
# consider this work to be licensed as "CC0 Universal", any version you choose.

# Variables in upper case are user configurables.

# Do this absolutely first
starttime=$(date +%s)
# Paranoia
starttime=$(($starttime-5))

# Utility functions

db () { (( VERBOSE >= 1 )) && echo '>>' $* }
db2 () { (( VERBOSE >= 2 )) && echo '>>>>' $* }
db3 () { (( VERBOSE >= 3 )) && echo '>>>>>>' $* }
db4 () { (( VERBOSE >= 4 )) && echo '>>>>>>>>' $* }
sep () { (( VERBOSE >= 2 )) && echo '============================================================' }

finish () {
    if (( $VERBOSE >= 1 )); then
        echo -n 'Mirror finished: '
        date
    fi
    exit 0
}

do_rsync () {
    # Needs three options: source, destination and the name of an array
    # containing additional options.  If rsync fails, this will sleep with
    # exponential backoff ten times, and then will exit the script.
    local src=$1
    local dest=$2
    local opts=$3
    local sleep=1
    while true; do
        # ZSHISM:  (P) flag to act on a variable by name.  Sadly, bash has
        # broken array handling.   bash 4.3 has local -n for this.  Older bash
        # needs hacks, or eval.  More info:
        # https://stackoverflow.com/questions/1063347/passing-arrays-as-parameters-in-bash
        # Or just use a freaking global.

        # Redirect to files based on VERBOSE
        # 0 - stdout and stderr redirected; dump stderr at end after ten tries
        # 1 - stdout redirected; stderr not redirected
        # 2 - Only some of stdout (the summar line at the end of the transfer)
        # 5 - Now start showing more output.  What to show?
        if (( VERBOSE >= 5 )); then
            db Calling $RSYNC $RSYNCOPTS ${(P)src} $src $dest
            $RSYNC $RSYNCOPTS ${(P)src} $src $dest
        elif (( VERBOSE >= 2 )); then
            db Calling $RSYNC $RSYNCOPTS ${(P)src} $src $dest \>\> $log
            $RSYNC $RSYNCOPTS ${(P)src} $src $dest

        fi
        rsyncreturn=$?

        (( rsyncreturn != 5 && rsyncreturn != 10 )) && break

        if (( VERBOSE >= 1 )); then
            (>&2 cat $errlog)
            rm $errlog
        fi

        db2 rsync failed: sleeping for $sleep
        sleep $sleep
        sleep=$(( sleep*2 ))
        if (( sleep > 1000 )); then
            (>&2 echo "Could not sync from $REMOTE/$module")
            (>&2 cat $errlog)
            exit 1
        fi

    done
}


# Parse args
while [[ $# > 0 ]]; do
    opt=$1
    case $opt in
        -a)
            alwayscheck=1
            ;;
        -c)
            cfgfile=$2
            shift
            if [[ ! -r $cfgfile ]]; then
                (>&2 echo Cannot read $cfgfile)
                exit 1
            fi
            ;;
        -d)
            verboseopt=$2
            shift
            ;;
        -t)
            backdate=$2
            alwayscheck=1
            shift
            ;;
        -T)
            backdate=$(date -d "$2" +%s)
            alwayscheck=1
            shift
            ;;
        *)
            (>&2 echo "Unrecognized argument.")
            exit 1
            ;;
    esac
    shift
done

# Mapping from module names to directories under fedora-buffet
# ZSHISM (initialize associative array)
typeset -A MODULEMAPPING
MODULEMAPPING=(
    fedora-alt          alt
    fedora-archive      archive
    fedora-enchilada    fedora
    fedora-epel         epel
    fedora-secondary    fedora-secondary
    )

# Default arguments; override in quick-fedora-mirror.conf
VERBOSE=0

DESTD=
TIMEFILE=

REMOTE=rsync://dl.fedoraproject.org
FILELIST=fullfiletimelist

RSYNC=/usr/bin/rsync
#ZSHISM? (initialize array with unsplit args)
RSYNCOPTS=(-aSH -f 'R .~tmp~' --dirs --preallocate --fuzzy --delete-delay --delay-updates)

MASTERMODULE=fedora-buffet
MIRRORBUFFET=0
MODULES=(fedora-enchilada fedora-epel)

# Load up the configuration file
for file in $cfgfile /etc/quick-fedora-mirror.conf \
        ~/.config/quick-fedora-mirror.conf \
        $(dirname $0)/quick-fedora-mirror.conf \
        ./quick-fedora-mirror.conf; do
    if [[ -r $file ]]; then
        source $file
        break
    fi
done

# Override with the command-line option
[[ -n $verboseopt ]] && VERBOSE=$verboseopt

# Check that the required parameters were provided
if [[ -z $DESTD ]]; then
    (>&2 echo "You must define DESTD in your configuration file ($configfile).")
fi
if [[ -z $TIMEFILE ]]; then
    (>&2 echo "You must define TIMEFILE in your configuration file ($configfile).")
fi

# Find the previous mirror time, and backdate if necessary
LASTTIME=0
if [[ -r $TIMEFILE ]]; then
    source $TIMEFILE
fi
if [[ -n $backdate ]]; then
    LASTTIME=$backdate
fi

tempd=$(mktemp -d -t quick-mirror.XXXXXXXXXX)
trap "rm -rf $tempd" EXIT
cd $tempd

# Modify RSYNCOPTS based on verbosity
# ZSHISM? (;& explicit case fallthrough)
case $VERBOSE in
    8)
        echo Loads of debugging.
        set -x
        ;&
    7)
        RSYNCOPTS+=(--progress)
        ;&
    6)
        echo Times:
        echo LASTTIME=$LASTTIME
        echo starttime=$starttime
        echo TIMEFILE=$TIMEFILE
        echo Dirs:
        echo tempd=$tempd
        echo DESTD=$DESTD
        echo Rsync:
        echo REMOTE=$REMOTE
        echo MASTERMODULE=$MASTERMODULE
        echo RSYNC=$RSYNC
        echo RSYNCOPTS=$RSYNCOPTS
        echo Modules:
        echo MODULES=$MODULES
        echo MODULEMAPPING=$MODULEMAPPING
        echo
        ;&
    5)  RSYNCOPTS+=(-v)
        ;&
    4)  RSYNCOPTS+=(-v)
        ;&
    3)
        ;&
    2)  echo -n 'Mirror starting: '; date
        ;;
    1)  echo -n 'Mirror starting: '; date
        ;&
    0)  RSYNCOPTS+=(-q)
        ;;
esac

if [[ $MIRRORBUFFET -ne '0' ]]; then
    # We want to mirror everything, so save the admin from listing the
    # individual modules.
    # ZSHISM (get keys from an associative array with (k))
    MODULES=(${(k)MODULEMAPPING})
fi

# We have to accumulate a big list of everything that needs to be
# transferred from all of the modules
for module in $MODULES; do
    sep
    db2 Processing $module

    # ZSHISM? (associative array indexing)
    moduledir=$MODULEMAPPING[$module]
    totallines=0

    # First fetch each filefiletimelist, and make one big transfer list from
    # all of them.  Copy in the old file from our repo to speed up rsync in case
    # most of it didn't change
    rm -f $FILELIST
    if [[ -f $DESTD/$moduledir/$FILELIST ]]; then
        cp -p $DESTD/$moduledir/$FILELIST .
        filelisttime=$(stat --format=%Y $FILELIST)
    fi

    do_rsync $REMOTE/$module/$FILELIST . rsync-$module rsync-err-$module

    #sleep=1
    #while true; do
    #    $RSYNC $RSYNCOPTS $REMOTE/$module/$FILELIST .
    #    rsyncreturn=$?
    #    # rsync will return 5 if the host says it's too busy, and 10 if the
    #    # connection was refused or the host doesn't resolve.
    #    (( rsyncreturn != 5 && rsyncreturn != 10 )) && break
    #    db2 rsync failed: sleeping for $sleep
    #    sleep $sleep
    #    sleep=$(( sleep*2 ))
    #    if (( sleep > 1000 )); then
    #        (>&2 echo "Could not sync $REMOTE/$module/$FILELIST")
    #        exit 1
    #    fi
    #done

    if [[ -z $alwayscheck && $filelisttime == $(stat --format=%Y $FILELIST) ]]; then
        db2 No change in file list mtime.  Skipping $module.
        continue
    fi

    db2 ++ Extracting lists

    # Get files on the server which changed since the last mirror
    awk "{if (\$1 >= $LASTTIME) {print \"$moduledir/\" substr(\$0, 14)}}" < $FILELIST > newfiles-$module
    linecount=$(wc -l < newfiles-$module)
    db3 Found $linecount new or updated files in $module since last run.

    # We always want to transfer the file list itself in the transaction.
    echo $moduledir/$FILELIST >> newfiles-$module
    cat newfiles-$module >> master-transferlist
    totallines=$((totallines+linecount))

    # Dirs on the server which changed since the last mirror
    awk "{if (\$1 >= $LASTTIME && \$2 == \"d\") {print \"$moduledir/\" substr(\$0, 14)}}" < $FILELIST > newdirs-$module
    linecount=$(wc -l < newdirs-$module)
    db3 Found $linecount new or updated dirs in $module since last run.
    cat newdirs-$module >> master-transferlist
    totallines=$((totallines+linecount))

    # Get a list of all files which should be in the repository
    awk "{if (\$2 == \"f\" || \$2 == \"l\") {print \"$moduledir/\" substr(\$0, 14)}}" < $FILELIST > allfiles-$module
    linecount=$(wc -l < allfiles-$module)
    db3 Found $linecount remote files in $module.

    # Get a list of all of the directories which should be in the repository
    awk "{if (\$2 == \"d\") {print \"$moduledir/\" substr(\$0, 14)}}" < $FILELIST > alldirs-$module
    linecount=$(wc -l < alldirs-$module)
    db3 Found $linecount remote dirs in $module.

    # We want to delete any files, symlinks or directories  which exist locally
    # but which don't exist on the server.  If we do this here, before the
    # transfer, the directory mtimes will be correct after the transfer.
    if [[ -d $DESTD/$moduledir ]]; then
        db2 ++ Generating local dir lists
        pushd $DESTD
        find $moduledir/* -type d > $tempd/localdirs-$module
        popd

        linecount=$(wc -l < localdirs-$module)
        db3 Found $linecount local dirs in $module.

        # Find dirs on the client which don't exist on the server
        sort alldirs-$module alldirs-$module localdirs-$module | uniq -u > deletedirs-$module
        linecount=$(wc -l < deletedirs-$module)
        db2 ++ Will remove $linecount stale dirs from $moduledir.
        cat deletedirs-$module >> master-deletedirs

        # Find dirs on the server which are missing on the client
        sort localdirs-$module localdirs-$module alldirs-$module | uniq -u > missingdirs-$module
        linecount=$(wc -l < missingdirs-$module)
        db3 Found $linecount missing dirs in $module.
        cat missingdirs-$module >> master-transferlist
        totallines=$((totallines+linecount))

        db2 ++ Generating local file lists
        pushd $DESTD
        db4 find $moduledir/* -type f -o -type l '>' $tempd/localfiles-$module
        find $moduledir/* -type f -o -type l > $tempd/localfiles-$module
        popd
        linecount=$(wc -l < localfiles-$module)
        db3 Found $linecount local files in $module.

        # Find files on the client which don't exist on the server
        sort allfiles-$module allfiles-$module localfiles-$module | uniq -u > deletefiles-$module
        linecount=$(wc -l < deletefiles-$module)
        db2 ++ Will remove $linecount stale files from $module.
        cat deletefiles-$module >> master-deletefiles

        # Find files on the server which are missing on the client
        sort localfiles-$module localfiles-$module allfiles-$module | uniq -u > missingfiles-$module
        linecount=$(wc -l < missingfiles-$module)
        db3 Found $linecount missing files in $module.
        cat missingfiles-$module >> master-transferlist
        totallines=$((totallines+linecount))

    fi
    db2 Finished processing.  Added $totallines files and dirs to the transfer list.
done

if [[ ! -e master-transferlist ]]; then
    db2 No changed files.
    finish
fi

# rsync prefers sorted file lists and will complain if there are duplicates
sort -u master-transferlist > master-transferlist.sorted
linecount=$(wc -l < master-transferlist.sorted)
sep; sep
db2 Beginning transfer of $linecount files.

# Now we have a list of everything which has changed recently in every module
# we want, pass that to rsync (non recursive mode!) and it should transfer just
# the changed files without having to pull the entire huge file list.
db3 $RSYNC $RSYNCOPTS --files-from master-transferlist.sorted $REMOTE/$MASTERMODULE/ $DESTD
sleep=1
while true; do
    $RSYNC $RSYNCOPTS --files-from master-transferlist.sorted $REMOTE/$MASTERMODULE/ $DESTD
    rsyncreturn=$?
    (( rsyncreturn != 5 && rsyncreturn != 10 )) && break
    db2 rsync failed: sleeping for $sleep
    sleep $sleep
    sleep=$(( sleep*2 ))
    if (( sleep > 1000 )); then
        (>&2 echo "Could not sync files from $REMOTE/$module")
        exit 1
    fi
done

if [[ -s master-deletedirs ]]; then
    db2 Removing stale directories.
    for nuke in $(cat master-deletedirs); do
        if [[ -d "$DESTD/$nuke" ]]; then
            db4 Removing $nuke
            rm -rf "$DESTD/$nuke"
        fi
    done
fi

# If we had deleted the directories before making the list of stale files, we
# could save a lot of work here.  But since we have to delay the process, most
# of these will probably have been deleted above.
if [[ -s master-deletefiles ]]; then
    db2 Removing stale files.
    for nuke in $(cat master-deletefiles); do
        if [[ -f "$nuke" || -h "$nuke" ]]; then
            db4 Removing $nuke
            rm -f "$DESTD/$nuke"
        fi
    done
fi

db2 Saving mirror time to $TIMEFILE
if [[ -e $TIMEFILE ]]; then
    mv $TIMEFILE $TIMEFILE.prev
fi
echo LASTTIME=$starttime > $TIMEFILE

if (( ? != 0 )); then
    (>&2 echo Problem saving timestamp file $TIMEFILE)
    exit 1
fi

finish
